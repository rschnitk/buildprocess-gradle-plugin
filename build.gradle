plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'com.ceyoniq.gradle.buildprocess'
version = '1.0.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation gradleApi()
}

tasks.compileJava {
	options.encoding = 'UTF-8'
	sourceCompatibility = 11
	targetCompatibility = 11
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addBooleanOption('html5', true);
}

gradlePlugin {
    website = 'https://www.ceyoniq.com'
    vcsUrl = 'https://github.com/Schnitker/gradle-scripts'
	plugins {
		buildprocess {
			id = 'com.ceyoniq.gradle.buildprocess'
			implementationClass = 'com.ceyoniq.gradle.buildprocess.BuildprocessPlugin'
			displayName = "Ceyoniq buildprocess tasks"
			description = "Ceyoniq buildprocess tasks: version extension and bom upload"
			tags.set( [ "ceyoniq", "bom upload", "version" ] )
		}
	}
}

publishing {
    repositories {
        if ( System.getenv('CI_JOB_TOKEN') != null ) { // test Gitlab CI
	        maven {
	            name = "gitlab"
	            url = "https://source.ceyoniq.com/api/v4/projects/${System.getenv('CI_PROJECT_ID')}/packages/maven"
	            credentials(HttpHeaderCredentials) {
	                name = "Job-Token"
	                value = System.getenv('CI_JOB_TOKEN')
	            }
	            authentication {
	                header(HttpHeaderAuthentication)
	            }
	        }
        } else { // local        
            maven {
                url "file:///${rootProject.projectDir}/upload"
            }
        }
    }
}