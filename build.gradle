apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

group = 'com.ceyoniq.gradle.buildprocess'
version = '1.0.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	implementation gradleApi()
}

tasks.compileJava {
	options.encoding = 'UTF-8'
	sourceCompatibility = 11
	targetCompatibility = 11
}

java {
    withSourcesJar()
}

gradlePlugin {
	plugins {
		buildprocess {
			id = 'org.ceyoniq.gradle.buildprocess'
			implementationClass = 'com.ceyoniq.gradle.buildprocess.BuildprocessPlugin'
		}
	}
}

publishing {
    repositories {

        if ( System.getenv('CI_JOB_TOKEN') != null ) { // test Gitlab CI
	        maven {
	            name = "gitlab"
	            url = "https://source.ceyoniq.com/api/v4/projects/${System.getenv('CI_PROJECT_ID')}/packages/maven"
	            credentials(HttpHeaderCredentials) {
	                name = "Job-Token"
	                value = System.getenv('CI_JOB_TOKEN')
	            }
	            authentication {
	                header(HttpHeaderAuthentication)
	            }
	        }
        } else { // local        
            maven {
                url "file:///${rootProject.projectDir}/upload"
            }
        }
    }

    publications {
        mavenJava( MavenPublication ) {
            from components.java

            pom {
				name = 'Buildprocess Plugin'
				description = project.description
                developers {
					developer {
						id = "AL"
						name = "AL development"
						organization = 'Ceyoniq.com'
						email = "FE-ApplicationLayer@ct.com"
					}
                }
            }
        }
    }            
    generatePomFileForMavenJavaPublication {
        destination = file( "$buildDir/libs/${project.name}-${project.version}.pom" )
    }

    generateMetadataFileForMavenJavaPublication {
        outputFile = file( "$buildDir/libs/${project.name}-${project.version}.module" )
    }
}

// task for artifacts/archives reference
tasks.register("generatePom") {
    dependsOn generatePomFileForMavenJavaPublication
    group = "Build"
    description = "Build maven pom artefact."
    ext.destFile = generatePomFileForMavenJavaPublication.destination
}

// task for artifacts/archives reference
tasks.register("generateModule") {
    dependsOn generateMetadataFileForMavenJavaPublication
    group = "Build"
    description = "Build gradle module artefact."
    ext.destFile = generateMetadataFileForMavenJavaPublication.outputFile
}

assemble.dependsOn generatePom
assemble.dependsOn generateModule